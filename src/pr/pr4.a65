



         ;PR4 2.4 .S22 ==0801==
#if 1-fci
         ;zei =$1c
dir       ldx #$10
          jsr namedtp
          beq dirok
          rts 
dirok     stx zei
          sty zei+1
          jsr prtbtst
          beq diroo1
          jmp direx
diroo1    ldy #gnfile
          lda (zei),y
          sta dcl
          iny 
          lda (zei),y
          sta dcl+1
          lda zei
          clc 
          adc #gnpath
          sta dpa
          lda zei+1
          adc #0
          sta dpa+1
          clc 
          jsr sgdta
          sta zei
          sty zei+1
          lda #0
          sta dircount
          sta dircount+1
          lda #<dirstext
          ldy #>dirstext
          jsr textout
          clc 
          jsr sgdrive
          clc 
          adc #"A"             ; "a"
          jsr chout
          lda #":"
          jsr chout
          lda #<dirntext
          ldy #>dirntext
          jsr textout
          lda #<ddtp
          ldy #>ddtp
          sec 
          jsr sgdta
          lda #"*"
          sta ddtp+dsname
          sta ddtp+dsext
          lda #%00001000             ;disk-name
          sta ddtp+dsattr
          lda dcl
          ldx dcl+1
          ldy #0
          jsr sfirso
          bne dirnonam
          jsr dirnout
dirnonam  lda #13
          jsr chout
          lda dpa
          ldy dpa+1
          sta zei
          sty zei+1
          ldy #0
dirnn2    lda (zei),y
          beq dirnn1
          jsr chout
          iny 
          bne dirnn2
dirnn1    lda #%11110111
          sta ddtp+dsattr
          lda dcl
          ldx dcl+1
          ldy #0
          jsr sfirso
          jmp pnexte
pnext     jsr snext
pnexte    bne dirend
          inc dircount
          bne dira1
          inc dircount+1
dira1     jsr crout
          jsr dirnout
          jsr dirpout
          jmp pnext
dirend    cmp #ennotfnd
          bne direx
dirend1   jsr crout
          lda dircount+1
          jsr hexout
          lda dircount
          jsr hexout
          lda #<dirftext
          ldy #>dirftext
          jsr textout
          lda #<dword
          ldy #>dword
          jsr gtdskfr                ;anzahl freie bytes auf diskette
          bne direx
          ldy #3
ddfspo    lda dword,y
          jsr hexout
          dey 
          bpl ddfspo
          lda #<dirbtext
          ldy #>dirbtext
          jsr textout
          lda #0
direx     pha 
          jsr prtoff
          pla 
          rts 
dirnout   ldy #0
dir1      lda ddtp+dgname,y
          jsr chout
          iny 
          cpy #8
          bne dir1
          jsr spout
          ldy #0
dir2      lda ddtp+dgext,y
          jsr chout
          iny 
          cpy #3
          bne dir2
          rts 
dirpout   lda ddtp+dgattr
          and #%00010000
          beq dirsize
          ldy #>dirdtext
          lda #<dirdtext
          jsr textout
          jmp dirdate
dirsize   jsr spout
          lda ddtp+dgsize+3
          jsr hexout
          lda ddtp+dgsize+2
          jsr hexout
          lda ddtp+dgsize+1
          jsr hexout
          lda ddtp+dgsize+0
          jsr hexout
dirdate   jsr spout
          lda ddtp+dghour
          jsr dezbout
          lda #":"
          jsr chout
          lda ddtp+dgmin
          jsr dezbout
          lda #":"
          jsr chout
          lda ddtp+dgsec
          jsr dezbout
          jsr spout
          lda ddtp+dgday
          jsr dezbout
          lda #"."
          jsr chout
          lda ddtp+dgmonth
          jsr dezbout
          lda #"."
          jsr chout
          lda ddtp+dgyear
          clc 
          adc #80
          jmp dezbout
#endif
textout   sta zei
          sty zei+1
          ldy #0
texto1    lda (zei),y
          beq textor
          jsr chout
          inc zei
          bne texto1
          inc zei+1
          bne texto1
textor    rts 
#if fci-1
hexout    pha 
          lsr 
          lsr 
          lsr 
          lsr 
          jsr binout
          pla 
          and #$0f
binout    clc 
          adc #"0"
          cmp #"9"+1
          bcc bino1
          adc #6
bino1     jmp chout
dezbout   sta dint1
          lda #0
          sta dint1+1
          lda #10
          ldx #0
          jsr ddiv
          clc 
          adc #"0"
          jsr chout
          lda dint1
          clc 
          adc #"0"
          jmp chout
         ;**** dint1 / a/x = a/x rest dint1
ddiv      stx dint2+1
          sta dint2
          sty dabuf
          ldx #1
          lda #0
          sta dint3
          sta dint3+1
ddiv1     lda dint2
          asl 
          tay 
          lda dint2+1
          rol 
          bcs ddiv3
          cmp dint1+1
          bcc ddiv2
          bne ddiv3
          cpy dint1
          bcc ddiv2
          bne ddiv3
ddiv2     sta dint2+1
          sty dint2
          inx 
          bne ddiv1
ddiv3     dex 
          bpl ddiv6
          ldy dabuf
          lda dint3
          ldx dint3+1
          rts 
ddiv6     lda dint1+1
          cmp dint2+1
          bcc ddiv4
          bne ddiv5
          lda dint1
          cmp dint2
          bcc ddiv4
ddiv5     lda dint1
          sec 
          sbc dint2
          sta dint1
          lda dint1+1
          sbc dint2+1
          sta dint1+1
          sec 
          .byt $24
ddiv4     clc 
          rol dint3
          rol dint3+1
          lsr dint2+1
          ror dint2
          jmp ddiv3
         ;****
         ;****
pcat                       ;
          jsr prtbtst
          sec 
          bne dce1
          lda #13
          jsr chout
          lda #"$"
          sta buf
          lda #1
          ldx #<buf
          ldy #>buf
          jsr setfnpar
          ldx iecunit
          lda #1
          ldy #0
          jsr setfpar
          jsr open
          ora #64
dce1      bcs dce
          lda #0
          sta status
          ldx #1
          jsr chkin
          ora #64
          bcs dce
          jsr basin
          jsr basin
dcloop    lda #0
          sta status
          jsr basin
          jsr basin
          lda status
          bne dcok
          jsr basin
          sta dint1
          jsr basin
          sta dint1+1
          jsr dezout
dcl1      jsr basin
          jsr chout
          cmp #0
          bne dcl1
          jsr clrch
          jsr crout
          ldx #1
          jsr chkin
          ora #64
          bcs dce
          jmp dcloop
dcok      lda #0
dce       pha 
          jsr prtoff
          jsr clrch
          lda #1
          jsr close
          pla 
          rts 
         ;****
dezout    ldy #0
do1       lda dhi,y
          tax 
          lda dlo,y
          jsr ddiv
          iny 
          cmp #0
          bne do2
          cpy #4
          bne do1
          beq do3
do2       clc 
          adc #"0"
          jsr chout
do3       lda dhi,y
          tax 
          lda dlo,y
          jsr ddiv
          clc 
          adc #"0"
          jsr chout
          iny 
          cpy #5
          bcc do3
          rts 
         ;********
pprint    lda prtfl
          eor #128
          sta prtfl
          bpl pprt1
          lda #<tpon
          ldy #>tpon
          jsr textout
          lda #0
          rts 
pprt1     
          lda #<tpoff
          ldy #>tpoff
          jsr textout
          lda #0
pca1      rts 
#endif
         ;****
prtbtst   lda prtdev
          ora #$80
          tax 
          bne prttst1
prttst    ldx prtdev
prttst1   lda prtfl
          bpl pttr
          lda prtfl
          ora #64
          sta prtfl
          txa 
          jmp chodev
pttr      lda #0
          rts 
         ;****
prtoff    lda prtfl
          and #191
          sta prtfl
          lda #0
          jmp chodev
#if fci-1
         ;********
pecho     sta czei
          sty czei+1
          jsr prttst
          bne pecr
          lda czei
          ldy czei+1
          jsr textout
          jsr crout
          lda #0
pecr      pha 
          jsr prtoff
          pla 
          rts 
         ;********
pprtdev   sta czei
          sty czei+1
          jsr gbyt
          bcs pca1
          stx prtdev
          jsr gbyt
          bcs pca1
          txa 
          and #63
          bit prtfl
          bpl ppd1
          ora #128
ppd1      sta prtfl
          lda #0
          rts 
         ;********
#endif
prtinit   lda #0
          sta prtfl
          ldx #<svtab
          ldy #>svtab
          jsr setvec
          rts 
         ;********
pchout    bit prtfl
          bvc pcor
          pha 
          lda prtfl
          lsr 
          bcc pco1
          pla 
          jsr bcbmasc
          jmp pco3
pco1      lsr 
          bcc pco2
          pla 
          jsr basccbm
          jmp pco3
pco2      pla 
pco3      
          cmp #10
          bne pco4
          lda prtfl
          and #%00110000
          eor #%00010000
          lsr 
          lsr 
          jmp pco4a
pco4      cmp #13
          bne pcor
          lda prtfl
          and #%00001100
pco4a     
          lsr 
          lsr 
          sta prtdiv
          cmp #2
          bcc pco7
          lsr 
          lda #10
          bcc pco6
          lda #13
pco6      
          jsr pcor
pco7      lda prtdiv
          lsr 
          lda #13
          bcc pco8
          lda #10
pco8      
pcor      jmp (vchout)
          .end 
