



         ;PR3 2.4 .S26 ==0801==
#if 1-fci
pu        jsr gbyt
          bne pue
          txa 
          cmp #16
          bcc pu1
          sbc #6
pu1       and #15
          sta iecunit
          lda #0
pue       rts 
pq        jsr gdrv
          bne perr
          txa 
          jsr query
          pha 
          stx zei
          sty zei+1
          jsr crout
          ldy #0
pq1       lda (zei),y
          jsr hexout
          jsr spout
          iny 
          cpy #6
          bne pq1
          jsr crout
          pla 
perr      rts 
pc        jsr gdrv
          bne perr
          txa 
          jmp mediach
pb        jsr gdrv
          bne perr
          txa 
          jsr getbpb
          pha 
          stx zei
          sty zei+1
          ldy #0
pb1       lda (zei),y
          sta buffer,y
          iny 
          cpy #bpplen
          bne pb1
          pla 
prwea     rts 
pr        lda #0
          .byt $2c
pw        lda #1
          sta pblock+pbrwflag
          jsr gdrv
          beq prw1
          cmp #enilldrv
          bne prwea
          stx pblock+pbside
          clc 
          jsr sgdrive
          sta pblock+pbdrive
          jmp prw2
prw1      stx pblock+pbdrive
          jsr gbyt
          bne prwea
          stx pblock+pbside
prw2      jsr gbyt
          bne prwea
          stx pblock+pbtrack
          jsr gbyt
          bne prwea
          bcs prwea
          stx pblock+pbsector
          lda #<buffer
          sta pblock+pbbuffer
          lda #>buffer
          sta pblock+pbbuffer+1
          lda #prgbank
          sta pblock+pbbank
          lda #<pblock
          ldy #>pblock
          jmp floprw
pra       lda #0
          .byt $2c
pwa       lda #1
          sta pblock+aprwflag
          jsr gadr
          beq prwa1
          rts 
prwa1     stx pblock+aprecord
          sty pblock+aprecord+1
          clc 
          jsr sgdrive
          sta pblock+apdrive
          ldy #0
          sty pblock+apanz+1
          iny 
          sty pblock+apanz
          lda #<buffer
          sta pblock+apbuffer
          lda #>buffer
          sta pblock+apbuffer+1
          lda #prgbank
          sta pblock+apbank
          lda #<pblock
          ldy #>pblock
          jmp rwabs
pm        jsr gadr
          stx fad2
          sty fad2+1
          txa 
          clc 
          adc #<buffer
          sta zei
          tya 
          adc #>buffer
          sta zei+1
          jsr gadr
          bcs pme
          
          txa 
          clc 
          adc #<buffer
          sta fadr
          tya 
          adc #>buffer
          sta fadr+1
          jsr prttst
pmout     lda #">"
          jsr chout
          lda #";"
          jsr chout
          lda fad2+1
          jsr hexout
          lda fad2
          jsr hexout
          jsr spout
          ldy #0
pm1       lda (zei),y
          jsr hexout
          jsr spout
          iny 
          cpy #8
          bcc pm1
          ldy #0
pm2       lda (zei),y
          tax 
          and #127
          cmp #32
          bcs pm3
          ldx #"."
pm3       txa 
          jsr chout
          iny 
          cpy #8
          bcc pm2
          jsr crout
          lda #8
          clc 
          adc fad2
          sta fad2
          bcc pm4
          inc fad2+1
pm4       lda #8
          clc 
          adc zei
          sta zei
          bcc pm5
          inc zei+1
pm5       lda zei+1
          cmp fadr+1
          bcc pmout
          bne pmend
          lda zei
          cmp fadr
          bcc pmout
pmend     lda #0
pme       pha 
          jsr prtoff
          pla 
          rts 
pg        jsr gadr
          bne pge
          txa 
          clc 
          adc #<buffer
          sta zei
          tya 
          adc #>buffer
          sta zei+1
          lda #0
          sta fad2
pg1       jsr gbyt
          bne pge
          txa 
          ldy fad2
          sta (zei),y
          iny 
          sty fad2
          cpy #8
          bne pg1
pge       lda #0
          rts 
pcol      jsr gbyt
          stx clibcol
          jsr gbyt
          bcs pcol1
          stx clitcol
pcol1     lda clibcol
          jsr setbck
          lda clitcol
          jsr settxt
          lda #0
          rts 
gdati     jsr chrgot
          bne pda1
          clc 
          rts 
pda1      lda #$ff
          clc 
          adc czei
          sta czei
          lda #$ff
          adc czei+1
          sta czei+1
          lda #0
          sta laenge
          sta laenge+1
          sta laenge+2
          jsr dezbyt
          sta laenge
          bcs pda2
          jsr chrgot
          beq pda2
          jsr dezbyt
          sta laenge+1
          bcs pda2
          jsr chrgot
          beq pda2
          jsr dezbyt
          sta laenge+2
          bcs pda2
pda2      sec 
          ldx #<laenge
          ldy #>laenge
          rts 
ptime     jsr gdati
          bcc ptime1
          lda laenge
          pha 
          lda laenge+2
          sta laenge
          pla 
          sta laenge+2
          jmp sgtime
pdate     jsr gdati
          bcc ptime1
          lda laenge+2
          sec 
          sbc #80
          sta laenge+2
          sec 
          jmp sgdate
ptime1    lda #0
          rts 
dezbyt    lda #0
          sta fadr
          jsr numget
          bcs dezbe
          sta fadr
          jsr numget
          bcs dezbe
          pha 
          asl fadr
          lda fadr
          asl 
          asl 
          clc 
          adc fadr
          sta fadr
          pla 
          clc 
          adc fadr
          clc 
          bcc dezbn
dezbe     cmp #0
          bne dezbe1
          lda fadr
          sec 
          rts 
dezbe1    lda fadr
          clc 
          rts 
dezbn     php 
          pha 
dezbm     jsr numget
          bcc dezbm
          pla 
          plp 
          rts 
numget    jsr chrget
          beq numge
          cmp #"0"
          bcc numge
          cmp #$3a
          bcs numge
          and #$0f
          clc 
          rts 
numge     sec 
          rts 
prename   sta zei
          sty zei+1
          ldy #<-1
prn0      iny 
          lda (zei),y
          beq prne1
          cmp #" "
          beq prn0
          ldx #0
prn1      lda (zei),y
          sta dbuf,x
          beq prne1
          cmp #$5c             ;"\"
          beq prne1
          cmp #":"
prne1     beq prne
          cmp #" "
          beq prn1a
          iny 
          inx 
          bne prn1
prn1a     lda #0
          sta dbuf,x
          tya 
          clc 
          adc zei
          sta zei
          bcc mrnx
          inc zei+1
#endif
mrnx      ldy #<-1
prn2      iny 
          lda (zei),y
          beq prne
          cmp #" "
          beq prn2
prn3      lda (zei),y
          beq prn4
          cmp #$5c             ;"\"
          beq prne
          cmp #":"
          beq prne
          iny 
          bne prn3
prn4      lda zei
          ldy zei+1
          ldx #$ff
          jsr namedtp
          beq prne2
          cmp #ennotfnd
          bne prnex
          clc 
          jsr sgdta
          sta rwbzei
          sty rwbzei+1
          ldy #dsname
prnx1     lda (rwbzei),y
          sta ddtp,y
          iny 
          cpy #dsname+12
          bcc prnx1
          lda #<dbuf
          ldy #>dbuf
          ldx #$ff
          jsr namedtp
          bne prnex
          ldx #dsname
          ldy #dgname
prnx2     lda ddtp,x
          sta (rwbzei),y
          inx 
          iny 
          cpy #dgname+11
          bcc prnx2
          jmp wrentry
prne      lda #enillnam
          .byt $2c
prne2     lda #enfexist
prnex     rts 

#if fmn-1
mrename   lda #64
          ldx #6
          jsr mdirpin
          beq mrn1
mrnr      rts 
mrn1      
          lda lanz
          beq mrnr
          jsr lzeibuf
          sta zei
          sty zei+1
          ldy #0
mrn2      lda (zei),y
          sta dbuf,y
          beq mrn3
          iny 
          bne mrn2
mrn3      
          lda #14
          jsr prntcmd
          lda #<isnpt
          ldy #>isnpt
          jsr winput
          bcs mrnr
          stx zei
          sty zei+1
          jmp mrnx
#endif
          .end 
