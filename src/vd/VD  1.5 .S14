



         ;../origsrc/bdos+s1/VD  1.5 .S14 ==0801==
          .word $8800
         prg=1
         bdos=$a000

dinit     sta rwbzei
          sty rwbzei+1
          ldy #<-1
          sty dosd0
          sty dosd1
          ldx #0
          jsr gipar
          bcs dii2
          inx 
          jsr gipar
dii2      ldx #5
dii3      lda dosd0,x
          sta $0100,x
          dex 
          bpl dii3
          ldx #<svtab
          ldy #>svtab
          jsr setvec
pinit     lda #"u"
          sta media0
          sta media1
          sta cmdbuf
          lda #"0"
          sta cmdbuf+1
          lda dosd0
          cmp dosd1
          beq pina2
          lda iecd0
          cmp iecd1
          bne pina1
          lda drvd0
          cmp drvd1
          bne pina1
pina2     lda #<-1
          sta dosd1
pina1     
          lda dosd0
          bmi pinit5
          lda iecd0
          jsr opentest
          bcc pinit5
          lda #<-1
          sta dosd0
pinit5    lda dosd1
          bmi pinit3
          lda dosd0
          bmi pinit6
          lda iecd0
          cmp iecd1
          beq pinit3
pinit6    
          lda iecd1
          jsr opentest
          bcc pinit3
          lda #<-1
          sta dosd1
pinit3    lda #0
          rts 
pinit1    lda #endnrdy
          rts 
         ;*********
pexit     jsr clrch
          lda iecd0                ;s.o. 
          jsr close
          lda iecd1                ;s.o. 
          jsr close
          jmp (vexit)
         ;*********
getdpar   cmp dosd1
          beq gdp1
          cmp dosd0
          bne gdpe
          ldx #0
          .byt $2c
gdp1      ldx #1
          lda iecd0,x
          sta actunit
          lda drvd0,x
          clc 
          rts 
gdpe      sec 
          rts 
         ;*********
opentest  pha 
          tax 
          ldy #15
          jsr setfpar
          lda #0
          jsr setfnpar
          jsr open
          pla 
          bcs opt1
          pha 
          tax 
          jsr ckout
          pla 
          bcs opt2
          jsr clrch
          clc 
opt1      rts 
opt2      jsr close
          sec 
          rts 
         ;*********
gip1 gipar iny 
          lda (rwbzei),y
          beq gipe
          cmp #" "
          beq gip1
          sec 
          sbc #"a"
          sta dosd0,x
gip2      iny 
          lda (rwbzei),y
          beq gipe
          cmp #":"
          beq gip2
          and #15
          sta iecd0,x
          iny 
          lda (rwbzei),y
          beq gipe
          cmp #"0"
          bcc gip3
          cmp #"9"+1
          bcs gip3
          and #15
          sta actunit
          lda iecd0,x
          bne gip4
          lda #0
          .byt $2c
gip4      lda #10
          clc 
          adc actunit
          sta iecd0,x
gip5      iny 
          lda (rwbzei),y
          beq gipe
gip3      cmp #","
          beq gip5
          and #1
          sta drvd0,x
          clc 
          rts 
gipe      sec 
          rts 
         ;*********
pquery    jsr getdpar
          bcc pquery0
          jmp (vquery)
pquery0   tay 
          lda #0
          sta media0,x
          tya 
          bne pquery1
          lda #%00001010             ; drive 0
          .byt $2c
pquery1   lda #%00001011          ; drive 1
          ldy #2
          jsr send3cmd
          bcs bsnotrdy
          bit serial
          bvc bsslow
          jsr serin
          ldy #0
          jsr togclk
          jsr wbyte
          sta retbuf,y
          sta stat
          jsr togclk
          lda stat
          bpl bsnotmfm                ; gcr-format
          and #$0f
          cmp #2
          bcs bsdsker
          ldx #5
          iny 
init2     jsr wbyte
          sta retbuf,y
          jsr togclk
          iny 
          dex 
          bne init2
          lda stat
          and #15
          cmp #2
          bcs bsdsker
          ldx #<retbuf
          ldy #>retbuf
          jmp error0
notready bsnotrdy lda #entxrx
          .byt $2c
bsslow    lda #enslow
          .byt $2c
bsnotmfm  lda #engcr
          .byt $2c
error0 bserr0 lda #0
diskerr bsdsker pha 
          jsr clkhi
          pla 
          cli 
          rts 
         ;**********
pmediach  jsr getdpar
          bcc pmed0
          jmp (vmediach)
pmed0     tay 
          lda media0,x
          bne mediafmt
          tya 
          bne pmed1
          lda #%11001100
          .byt $2c
pmed1     lda #%11001101
         ; liefert 1 wenn disk gewechselt
pmedia1   jsr send3cmd             ; sonst 0
          bcs bsnotrdy
          bit serial
          bvc bsslow
          jsr serin
          jsr togclk
          jsr wbyte
          pha 
          jsr clkhi
          pla 
          sta stat
          and #$0f
          cmp #2
          bcc bserr0
          ldx #0
          cmp #11
          bne bsdsker
mediafmt  lda #1
          jmp diskerr
         ;**********
pfloprw   sta rwbzei
          sty rwbzei+1
          ldy #pbdrive
          lda (rwbzei),y
          jsr getdpar
          bcc pflop0
          lda rwbzei
          ldy rwbzei+1
          jmp (vfloprw)
pflop0    sta int1
          ldy #pbrwflag
          lda (rwbzei),y
          sta rwflag
          ldy #pbbank
          lda (rwbzei),y
          sta rwfbank
          lda stat
          beq prwq
          cmp #11
          bne prw0
prwq      ldy #pbdrive
          lda (rwbzei),y
          jsr query
          beq prw0
          rts 
prw0      ldx int1
          ldy #pbside
          lda (rwbzei),y
          cmp #1
          lda #%00000000
          bcc prw1
          ora #%00010000
prw1      cpx #1
          bcc prw1a
          ora #1             ; drive
prw1a     ldx rwflag
          beq prw2
          ora #2
prw2      sta cmdbuf+2
          ldy #pbtrack
          lda (rwbzei),y
          sta cmdbuf+3
          ldy #pbsector
          lda (rwbzei),y
          sta cmdbuf+4
          lda #1
          sta cmdbuf+5
          ldy #6
          jsr sendcmd
          bcc prwo1
          jmp notready
prwo1     bit serial
          bvs prwoo1
          jmp bsslow
prwoo1    ldy #pbbuffer
          lda (rwbzei),y
          iny 
          tax 
          lda (rwbzei),y
          sta rwbzei+1
          stx rwbzei                ; zeiger auf puffer
          lda stat
          lsr 
          lsr 
          lsr 
          lsr 
          and #3
          tay 
          lda length,y
          tax 
          lda rwflag
          bne prwrite
         ;*****
         ; read
          jsr serin
          jsr readerr
          sta stat
          and #$0f
          cmp #2
          bcc prrok
          jmp diskerr
prrok     stx cnt
          lda #<rwbzei
          jsr bsadr
          jsr togclk
pr1       ldy #0
pr2       jsr wbyte
          ldx rwfbank
          jsr bstash
          jsr togclk
          iny 
          bpl pr2
          tya 
          clc 
          adc rwbzei
          sta rwbzei
          bcc pr3
          inc rwbzei+1
pr3       dec cnt
          bne pr1
          jmp prwok
         ;*****
         ; write
prwrite   jsr serout
          lda #clkin
          sta rwflag
          stx cnt
prww0     lda #<rwbzei
          jsr bfadr
          ldy #0
prww1     lda dra
          cmp dra
          bne prww1
          and #clkin
          eor rwflag
          beq prww1
          lda rwflag
          eor #clkin
          sta rwflag
          ldx rwfbank
          jsr bfetch
          jsr sbyte
          iny 
          bpl prww1
          tya 
          clc 
          adc rwbzei
          sta rwbzei
          bcc prww2
          inc rwbzei+1
prww2     dec cnt
          bne prww0
          jsr serin
          jsr clklo
          jsr wbyte
          sta stat
          jsr clkhi
          lda stat
          and #$0f
          cmp #2
          bcc prwok
          jmp diskerr
         ;*****
prwok     ldx rwbzei
          ldy rwbzei+1
          jmp error0
         ;**********
pformat   cpx #2
          bcc pfora
          jmp (vformat)
pfora     stx ffmbuf
          sta pblock+pbdrive
          jsr getdpar
          bcc pfor0
          jmp (vformat)
pfor0     pha 
          ldy #$13
pfo1      lda bef,y
          sta cmdbuf,y
          dey 
          bpl pfo1
          pla 
          beq pfo2
          lda cmdbuf+2
          ora #$01
          sta cmdbuf+2
pfo2      
          ldx fformat
          bne pfoo3
          lda cmdbuf+2
          and #255-32
          sta cmdbuf+2          ; 1-seitig
pfoo3     ldy #$14
          jsr sendcmd
          lda pblock+pbdrive
          ldy #4
          jsr sfrbuf
          stx ffmbuf
          beq ffm1
          rts 
ffm1      lda pblock+pbdrive
          jsr query
          bne pfoerr2
          lda #0
          tax 
          ldy ffmbuf
          jsr bfzset
          lda fformat
          beq pfoo5
          ldy #0
pfo3      lda boots,y
          jsr bfset
          iny 
          cpy #32
          bne pfo3
          beq pfoo4
pfoo5     ldy #0
pfoo6     lda boot1,y
          jsr bfset
          iny 
          cpy #32
          bne pfoo6
pfoo4     ldy #256-32
          ldx #1             ;1 block+224 byte
          lda #0
ffm1a     jsr bfset
          dey 
          bne ffm1a
          ldy #254
          dex 
          bpl ffm1a
          lda #0
          sta int1
          sta int1+1
          ldx #0
          ldy #0
pfo4      iny 
          lda int1
          clc 
          adc boots,y
          sta int1
          dey 
          lda int1+1
          adc boots,y
          sta int1+1
          iny 
          iny 
          cpy #32
          bne pfo4
pfoerr2   bne pfoerr1
          lda int1
          sec 
          sbc #$33
          pha 
          lda int1+1
          sbc #$12             ;checksum-$1234+1
          jsr bfset
          pla 
          jsr bfset
          ldy #0
          sty pblock+pbside
          sty pblock+pbtrack
          iny 
          sty pblock+pbsector
          lda #prgbank
          sta pblock+pbbank
          lda #0
          tax 
          ldy ffmbuf
          jsr bfzset
          lda bf1z
          sta pblock+pbbuffer
          lda bf1z+1
          sta pblock+pbbuffer+1
          lda #1
          sta pblock+pbrwflag
          lda #<pblock
          ldy #>pblock
          jsr floprw
pfoerr1   bne pfoerr3
          ldx #1
          lda pblock+pbdrive
          cmp dosd0
          bne pfff1
          stx media0
          beq pfff2
pfff1     stx media1
pfff2     
          sec 
          jsr sgdrive
          bne pfoerr3
          lda #0
          tax 
          ldy ffmbuf
          jsr bfzset
          ldx #4
          ldy #0
          tya 
pfo6      jsr bfset
          iny 
          bne pfo6
          inc rwbzei+1
          dex 
          bne pfo6
          lda #0
          tax 
          ldy ffmbuf
          jsr bfzset
          lda #$ff
          jsr bfset
          jsr bfset
          jsr bfset
          lda #1
          ldx #0
          ldy ffmbuf
          sec 
          jsr bufabs
pfoerr3   bne pfoerr
          lda #3
          ldx #0
          ldy ffmbuf
          sec 
          jsr bufabs
          bne pfoerr
          lda #0
          tax 
          ldy ffmbuf
          jsr bfzset
          lda #0
          jsr bfset
          jsr bfset
          jsr bfset
          lda #2
          ldx #0
          ldy ffmbuf
          sec 
          jsr bufabs
          lda #4
          sta int1+2
pfo7      lda int1+2
          ldx #0
          ldy ffmbuf
          sec 
          jsr bufabs
          bne pfoerr
          inc int1+2
          lda int1+2
          cmp #13
          bne pfo7
          lda #0
pfoerr    pha 
          ldx ffmbuf
          jsr rlbuf
          pla 
          rts 
         ;********** uebertragung
send3cmd  sta cmdbuf+2
          ldy #3
         ; y= laenge des befehls in cmdbuf
sendcmd   lda serial
          and #%10111111
          sta serial
          ldx actunit
          jsr ckout
          bcc sendc0
          jmp notready
sendc0    ldx #0
sendc1    lda cmdbuf,x
          jsr bsout
          inx 
          dey 
          bne sendc1
          jsr clrch
          clc 
          rts 
togclk    lda dra
          eor #clkout
          sta dra
          rts 
wbyte     lda #8
wbyt1     bit icr
          beq wbyt1
          lda sdr
          rts 
sbyte     sta sdr
          lda #8
sbyt1     bit icr
          beq sbyt1
          rts 
clkhi     lda dra
          and #ff-clkout
          sta dra
          rts 
clklo     lda dra
          ora #clkout
          sta dra
          rts 
serin     sei 
          lda mmureg
          and #$f7
          sta mmureg
          lda cra
          and #$80
          ora #$08
          sta cra
          bit icr
          rts 
serout    sei 
          lda mmureg
          ora #8
          sta mmureg
          lda #0
          sta timah
          lda #3
          sta timal
          lda #$7f
          sta icr
          lda cra
          and #$80
          ora #$55
          sta cra
          bit icr
          rts 
readerr   jsr togclk
          jsr wbyte
          rts 
         ;**********
length    .byt 1,2,4,8; tabelle der sektor-laengen in 128 byte
         ;**********
bef       .byt "u","0",$26,$c1,0,2,39,9,0,0
          .byt $e5,1,2,3,4,5,6,7,8,9
         ;**********
boots                      ; bootsektor ibm pc
          .byt $eb,$2c,$90,0,0,0,0,0
          .byt $c0,$12,$80
          .word 512
          .byt 2
          .word 1
          .byt 2
          .word 112
          .word 720
          .byt 253
          .word 2
          .word 9
          .word 2
          .word 0,0
         ;****
boot1                      ; bootsektor ibm pc 1-seitig
          .byt $eb,$2c,$90,0,0,0,0,0
          .byt $c0,$12,$80
          .word 512
          .byt 1
          .word 1
          .byt 2
          .word 64
          .word 360
          .byt 252
          .word 2
          .word 9
          .word 1
          .word 0,0
ffmbuf    .byt 0
         ;****
svtab     
          .word anznew
          .word jmpadr
          .word jmpnew
          .word jmpold
         anznew=5
jmpadr    
          .word (query-bdos)*2/3
          .word (mediach-bdos)*2/3
          .word (floprw-bdos)*2/3
          .word (flopfmt-bdos)*2/3
          .word (exit-bdos)*2/3
jmpnew    
          .word pquery
          .word pmediach
          .word pfloprw
          .word pformat
          .word pexit
jmpold    
         ;.if # 
         ;.goto 10810

vquery    .word 0
vmediach  .word 0
vfloprw   .word 0
vformat   .word 0
vexit     .word 0
         ;********* data fuer teil 1
         ;puffer fuer floppy-befehl
fformat   .byt 0
         ;puffer fuer floppy-befehl
cmdbuf    .byt "u","0",0,0,0,0,0,0,0,0
          .byt 0,0,0,0,0,0,0,0
          .byt 0,0
         ;puffer fuer antwort bei query
retbuf    .byt 0,0,0,0,0,0
         ;parameterblock f. floprw syscall
pblock    .byt 0,0,0,0,0
          .word 0
          .byt 0
         ;parameterblock fuer rwabs
abspar    .byt 0,0
          .word 0,0,0
          .byt 0
         ;statusbyte
stat      .byt 0
         ;flag fuer floprw
rwflag    .byt 0
rwfbank   .byt 0
pgbbuf    .byt 0
cnt       .byt 0
int1      .byt 0,0,0
media0    .byt 0
media1    .byt 0
dosd0     .byt 0           ;drive a
         
dosd1     .byt 1           ;drive b
         
iecd0     .byt 8           ;unit 8 on iec
iecd1     .byt 9           ;unit 9 on iec
drvd0     .byt 0           ;drive 0 on unit 8
drvd1     .byt 0           ;drive 0 on unit 9
actunit   .byt 0           ;unit on air
         varend=*
          .text 
          .word varend
          .end 
